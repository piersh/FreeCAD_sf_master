#the python library needs to be copied first so that PyQt4 can be fixed up
get_filename_component(PYTHON_LIB_DIR @PYTHON_LIBRARY@ PATH)
set(PY_VERSION "@PYTHON_VERSION_MAJOR@.@PYTHON_VERSION_MINOR@")
set(PYTHON_DIR ${PYTHON_LIB_DIR}/python${PY_VERSION})

message("Copying python into bundle...")

execute_process(COMMAND cmake -E copy_directory ${PYTHON_DIR} ${CMAKE_INSTALL_PREFIX}/@INSTALL_LIB_DIR@/python${PY_VERSION})

file(GLOB MODULES
     "${CMAKE_INSTALL_PREFIX}/@INSTALL_LIB_DIR@/*@CMAKE_SHARED_LIBRARY_SUFFIX@")
file(GLOB_RECURSE QTPLUGINS
     "${CMAKE_INSTALL_PREFIX}/@INSTALL_LIB_DIR@/plugins/*.dylib")
file(GLOB_RECURSE PIVY
     "${CMAKE_INSTALL_PREFIX}/@INSTALL_EXE_DIR@/pivy/*@CMAKE_SHARED_LIBRARY_SUFFIX@")
file(GLOB PARTDESIGN
    "${CMAKE_INSTALL_PREFIX}/@INSTALL_MOD_DIR@/PartDesign/*@CMAKE_SHARED_LIBRARY_SUFFIX@")
file(GLOB PYQT
     "${CMAKE_INSTALL_PREFIX}/@INSTALL_LIB_DIR@/python${PY_VERSION}/site-packages/PyQt4/*.so")

function(gp_item_default_embedded_path_override item default_embedded_path_var)
    set(path "@executable_path/../lib")
    
    #the PartDesign libs are in the Mod directory
    if(item MATCHES "PartDesign.*\\.so")
        set(path "@executable_path/../Mod/PartDesign")
    endif(item MATCHES "PartDesign.*\\.so")

    set(${default_embedded_path_var} ${path} PARENT_SCOPE)
endfunction(gp_item_default_embedded_path_override)

include(BundleUtilities)

set(LIBS ${MODULES} ${QTPLUGINS} ${PARTDESIGN} ${PIVY} ${PYQT})

fixup_bundle("@APPS@" "${LIBS}" "@DIRS@")


